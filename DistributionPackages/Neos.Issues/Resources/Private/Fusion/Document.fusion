prototype(Neos.Issues:Document) < prototype(Neos.Issues:Document.Mixin)


prototype(Neos.Issues:Document.Mixin) < prototype(Neos.Fusion:Component) {
    content = null
    headline = null
    lead = null
    description = null
    link = null
    linkText = 'Link to the issue on github.com'
    linkTarget = ${String.startsWith(this.link, 'http') ? '_blank' : false}
    siteName = ${q(site).property('title')}

    renderer = Neos.Neos:Page {
        head.stylesheets.bootstrap = afx`
            <link
                href='https://stackpath.bootstrapcdn.com/bootstrap/4.3.1/css/bootstrap.min.css'
                rel='stylesheet'
                integrity='sha384-ggOyR0iXCbMQv3Xipma34MD+dH/1fQ784/j6cY/iJTQUOhcWr7x9JvoRxT2MZw1T'
                crossorigin='anonymous' />
        `
        bodyTag.attributes {
            class = 'container'
            style = 'margin-top:50px;margin-bottom:50px;'
        }
        body = Neos.Fusion:Component {
            nested = ${props}

            renderer = afx`
                <nav class='navbar navbar-expand navbar-light bg-light my-5'>
                    <span class='navbar-brand'>{props.nested.siteName}</span>
                    <Neos.Neos:Menu
                        maximumLevels={1}
                        attributes.class='navbar-nav'
                        normal.attributes.class='nav-item'
                        active.attributes.class='nav-item active'
                        current.attributes.class='nav-item active'
                        @process.addClassToLink={String.replace(value, '<a ', '<a class="nav-link" ')}
                         />
                </nav>
                
                <div class='jumbotron' @if.render={props.nested.headline}>
                    <h1>{props.nested.headline}</h1>
                    <p class='lead' @if.render={props.nested.lead}>{props.nested.lead}</p>
                    <hr class='my-4' @if.render={props.nested.link} />
                    <a
                        @if.render={props.nested.link && props.nested.linkText}
                        href={props.nested.link}
                        class='btn btn-primary btn-lg'
                        target={props.nested.linkTarget}>
                            {props.nested.linkText}
                    </a>
                    <p class='mt-4 mb-0' @if.render={props.nested.description}><small>{props.nested.description}</small></p>
                </div>
                {props.nested.content}
            `
        }
    }
}
