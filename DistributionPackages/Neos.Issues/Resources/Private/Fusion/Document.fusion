prototype(Neos.Issues:Document) < prototype(Neos.Issues:Document.Mixin)


prototype(Neos.Issues:Document.Mixin) < prototype(Neos.Fusion:Component) {
    content = null
    headline = null
    lead = null
    description = null
    link = null
    linkText = 'Link to the issue on github.com'
    linkTarget = ${String.startsWith(this.link, 'http') ? '_blank' : false}
    siteName = ${q(site).property('title')}

    renderer = Neos.Neos:Page {
        head.stylesheets.bootstrap = afx`
            <link
                href='https://stackpath.bootstrapcdn.com/bootstrap/4.3.1/css/bootstrap.min.css'
                rel='stylesheet'
                integrity='sha384-ggOyR0iXCbMQv3Xipma34MD+dH/1fQ784/j6cY/iJTQUOhcWr7x9JvoRxT2MZw1T'
                crossorigin='anonymous' />
        `
        bodyTag.attributes {
            class = 'container'
            style = 'margin-top:50px;margin-bottom:50px;'
        }
        body = Neos.Fusion:Component {
            @apply.props = ${props}
            renderer = afx`
                <nav class='navbar navbar-expand navbar-light bg-light my-5'>
                    <span class='navbar-brand'>{props.siteName}</span>
                    <Neos.Neos:Menu
                        maximumLevels={1}
                        attributes.class='navbar-nav'
                        normal.attributes.class='nav-item'
                        active.attributes.class='nav-item active'
                        current.attributes.class='nav-item active'
                        @process.addClassToLink={String.replace(value, '<a ', '<a class="nav-link" ')}
                         />
                </nav>
                
                <div class='jumbotron' @if.render={props.headline}>
                    <h1>{props.headline}</h1>
                    <p class='lead' @if.render={props.lead}>{props.lead}</p>
                    <hr class='my-4' @if.render={props.link} />
                    <a
                        @if.render={props.link && props.linkText}
                        href={props.link}
                        class='btn btn-primary btn-lg'
                        target={props.linkTarget}>
                            {props.linkText}
                    </a>
                    <p class='mt-4 mb-0' @if.render={props.description}><small>{props.description}</small></p>
                </div>
                {props.content}
            `
        }
    }
}
